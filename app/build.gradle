apply plugin: 'com.android.application'

/**
 * 拉去git的最新记录hash
 */
def gitHash = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()

/**
 * 打包时间
 */
def buildTime = new Date().format("yyyy-MM-dd HH:mm:ss", TimeZone.getTimeZone("UTC"))

def updateApkName(variant) {
    def versionName = android.defaultConfig.versionName
    def newName = "";
    if (variant.buildType.name.equals('release')) {
        newName = "GHDemo_" + versionName + "_release.apk"
    } else if (variant.buildType.name.equals('debug')) {
        newName = "GHDemo_" + versionName + "_debug.apk"
    } else if (variant.buildType.name.equals('gray')) {
        newName = "GHDemo_" + versionName + "_gray.apk"
    }
    variant.getPackageApplication().outputScope.apkDatas.forEach { apkData ->
        //这个修改输出APK的文件名
        apkData.outputFileName = newName
    }
}

android {

    compileSdkVersion rootProject.ext.android['compileSdkVersion']
    buildToolsVersion rootProject.ext.android['buildToolsVersion']

    defaultConfig {
        /**
         * 是否拆成多个Dex,在buildType中也可以指定
         */
        multiDexEnabled true
        applicationId "com.android.test.demo"
        minSdkVersion rootProject.ext.android['minSdkVersion']
        targetSdkVersion rootProject.ext.android['targetSdkVersion']
        versionCode rootProject.ext.android['versionCode']
        versionName rootProject.ext.android['versionName']
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        //自定义
        buildConfigField "String", "GIT_HASH", "\"${gitHash}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""

        /**
         * 可以通过 ./gradlew assembleDebug -P test.prop.string.value=haah 动态指定该值
         */
        def stringValueKey = 'test.prop.string.value'
        def stringValue;
        
        /**
         * ./gradlew命令行是否带有参数
         */
        if (project.hasProperty(stringValueKey)) {
            println(stringValueKey + ": " + project.property(stringValueKey))
            stringValue = project.property(stringValueKey)
        } else {
            stringValue = project.test.prop.string.value
        }

        buildConfigField "String", "TEST_STRING_VALUE", "\"${stringValue}\""


        /**
         * 可以通过 ./gradlew assembleDebug -P test.prop.int.value=5000 动态指定该值
         */
        def intValueKey = 'test.prop.int.value'
        def intValue;
        if (project.hasProperty(intValueKey)) {
            println(intValueKey + ": " + project.property(intValueKey))
            intValue = Integer.parseInt(project.property(intValueKey))
        } else {
            intValue = Integer.parseInt(project.test.prop.int.value)
        }

        buildConfigField "int", "TEST_INT_VALUE", "${intValue}"
    }

    /**
     *
     */
    sourceSets {

        main {

            /**
             * java代码目录
             */
            java.srcDirs = [
                'src/main/java/'
            ]

            /**
             * assets文件目录
             */
            assets.srcDirs = [
                    'assets'
            ]

            /**
             * so文件目录
             */
            jniLibs.srcDirs = [
                    'libs',
                    'src/main/jniLibs'
            ]

            //jni目录设置
            jni.srcDirs  = [
                    'src/main/jni'
            ]

            /**
             * aidl文件目录
             */
            aidl.srcDirs = [

            ]
        }
    }

    /**
     * 项目类型(debug、release)
     */
    buildTypes {

        release {

            //name 'release'

            debuggable false

            /**
             * 混淆开关
             */
            minifyEnabled true

            /**
             * 是否去除未利用的资源，默认false，表示不去除
             */
            shrinkResources true

            /**
             * 改变apk的applicationId,若release版本和debug版本指定了不同的suffix,则编译出的两个apk可
             * 以在手机上共存，因为applicationId不同.
             *
             * 应用ID和包名是相互独立的。改变包名不会影响应用ID,反之亦然
             */
            applicationIdSuffix ".release"


            /**
             * 将混淆文件打包到arr包中，只对aar有效
             */
            //consumerProguardFiles 'proguard-android.txt'

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            /**
             * 如果没有提供混淆规则文件，则设置默认的混淆规则文件(SDK/tools/proguard/proguard-android.txt)
             */
            pseudoLocalesEnabled true

            /**
             * 签名配置
             */
            signingConfig signingConfigs.debug


            manifestPlaceholders = [
                    "com.android.test.holder.value": "this is release holder!!"
            ]
        }

        debug {

            //name 'debug'

            debuggable true

            minifyEnabled false


            shrinkResources false

            /**
             * 同上
             */
            applicationIdSuffix ".debug"

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'


            pseudoLocalesEnabled false

            signingConfig signingConfigs.debug

            manifestPlaceholders = [
                    "com.android.test.holder.value": "this is debug holder!!"
            ]
        }

        /**
         * 自定义build type, 将会出现在build variants中
         *
         * 可以使用命令编译: ./gradlew assembleGray
         */
        gray {
            debuggable false

            minifyEnabled true

            shrinkResources true

            applicationIdSuffix ".gray"

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            pseudoLocalesEnabled true

            signingConfig signingConfigs.debug


            manifestPlaceholders = [
                    "com.android.test.holder.value": "this is gray holder!!"
            ]
        }
    }

    android.applicationVariants.all { variant ->
        updateApkName(variant);
    }

    compileOptions {
        /**
         * java源码的版本
         */
        sourceCompatibility JavaVersion.VERSION_1_8

        /**
         * 生成的Java字节码版本
         */
        targetCompatibility JavaVersion.VERSION_1_8

        /**
         * 是否使用Java编写的Gradle新的增量模型
         */
        incremental true
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }

    //配置aapt选项
    aaptOptions {

        /**
         * 扩展文件不会以压缩的形式存储到apk。
         * 添加一个空的扩展,即。,设置noCompress”将禁用压缩所有文件。
         * 例如：aaptOptions //不用压缩的文件noCompress ‘pak’, ‘dat’, ‘bin’
         */
        noCompress 'pak', 'dat', 'bin', 'notice'

        /**
         * png类型的图片会被处理
         */
        cruncherEnabled true

        /**
         * 快速处理，可能需要更多的内存和CPU。int类型。
         * 默认0，值越大处理越快，需要的内存和CPU也越大。
         */
        cruncherProcesses 8
    }

    dexOptions {

        /**
         * java堆大小
         */
        javaMaxHeapSize "4g"
    }

    lintOptions {

        /**
         * 如果发现错误，是否终止编译，默认为true
         */
        abortOnError true

        /**
         * lint是否展示错误输出的完整路径，默认是从lint调用的相对路径
         */
        absolutePaths false
    }

    /**
     *
     */
    packagingOptions {

        /**
         * 符合first-pick模式的，打包的时候将包含在apk中
         */
        pickFirsts = [

        ]

        /**
         * 排除不打包的文件
         */
        /*excludes = [
                'readme.txt',
                'META-INF/NOTICE.txt'
        ]*/

        /**
         * 合并文件，打包进apk
         */
        merges = [

        ]
    }

}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])


    // Required for local unit tests (JUnit 4 framework)
    androidTestImplementation(rootProject.ext.dependencies['espresso'], {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation rootProject.ext.dependencies["junit"]

    //support
    implementation rootProject.ext.dependencies['appcompat-v7']
    implementation rootProject.ext.dependencies['design']
    implementation rootProject.ext.dependencies['fragment']

    //第三方基础组件
    implementation rootProject.ext.dependencies['okhttp']
    implementation rootProject.ext.dependencies['fastjson']

    implementation rootProject.ext.dependencies['swipebacklayout']

    //java相关
    implementation rootProject.ext.dependencies['guava']
}